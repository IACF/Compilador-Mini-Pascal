########################## Leonardo ######################

<programa> ::= 
	program <id> ; <corpo> .

<corpo> ::= 
	<declarações> <comando-composto>

########################## ITALO ##############################

<declarações> ::= (<declaração-de-variável>  ;)*

<declaração-de-variável> ::= 
	var <lista-de-ids> : <tipo>

<lista-de-ids> ::= <id> (, <id> )*

<tipo> ::= <tipo-agregado> | <tipo-simples>

<tipo-simples> ::= 
	integer 
	| real 
	| boolean

<tipo-agregado> ::= array [ <literal> .. <literal> ] of <tipo>

<literal> ::= 
	<bool-lit> 
	| <int-lit> 
	| <float-lit> 

<bool-lit> ::= true | false


############################################ Victor ###########################################

<comando-composto> ::= 
	begin <lista-de-comandos> end

<lista-de-comandos> ::= (<comando> ;)*

<comando> ::= 
	<id> <seletor> := <expressão>
	| if <expressão> then <comando> ( else <comando> | <vazio> )
	| while <expressão> do <comando>
	| <comando-composto>

<seletor> ::= ( "[" <expressão> "]" )*
	
<expressão> ::= <expressão-simples> (<op-rel> <expressão-simples> | <vazio>)
	
<op-rel> ::= 
	< 
	| > 
	| <= 
	| >= 
	| = 
	| <>

######################################## Leonardo #############################################

<expressão-simples> ::= <termo> (<op-ad> <termo>)*

<op-ad> ::= 
	+ 
	| - 
	| or


<termo> ::= <fator> (<op-mul> <fator>)* 

<op-mul> ::= 
	* 
	| / 
	| and
	
<fator> ::= 
	<variável> 
	| <literal> 
	| "(" <expressão> ")"
	
		
<vazio> ::= 
	ε
