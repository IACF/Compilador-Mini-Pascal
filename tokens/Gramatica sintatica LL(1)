<atribuição> ::=  <variável> := <expressão>

1 regra - LL(1);


<bool-lit> ::= true | false

first(1):
    true
    false

LL(1)

<comando> ::= 
	<atribuição> 
	| <condicional> 
	| <iterativo> 
	| <comando-composto>

<comando-composto> ::= 
	begin <lista-de-comandos> end

<condicional> ::= 
	if <expressão> then <comando> ( else <comando> | <vazio> )

<corpo> ::= 
	<declarações> <comando-composto>

<declaração-de-variável> ::= 
	var <lista-de-ids> : <tipo>

<declarações> ::= (<declaração-de-variável>  ;)*

<digito> ::= 
	0 
	| 1 
	| 2 
	| ...
	| 9

<expressão> ::= 
	<expressão-simples> 
	| <expressão-simples> <op-rel> <expressão-simples>
	
<expressão> ::= <expressão-simples> (<op-rel> <expressão-simples> | <vazio>)

<expressão-simples> ::= <termo> (<op-ad> <termo>)*
	
<fator> ::= 
	<variável> 
	| <literal> 
	| "(" <expressão> ")"
	 	
<id> ::= <letra>(<letra> | <digito>)* 

<int-lit> ::= <digit>(<digit>)*

<iterativo> ::= 
	while <expressão> do <comando>

<letra> ::= 
	a 
	| b 
	| c 
	| ... 
	| z
	
<lista-de-comandos> ::= (<comando> ;)*

<lista-de-ids> ::= <id> (, <id> )*
	
<literal> ::= 
	<bool-lit> 
	| <int-lit> 
	| <float-lit> 
<op-ad> ::= 
	+ 
	| - 
	| or
<op-mul> ::= 
	* 
	| / 
	| and
<op-rel> ::= 
	< 
	| > 
	| <= 
	| >= 
	| = 
	| <>
<outros> ::= 
	! 
	| @ 
	| # 
	| ...

<programa> ::= 
	program <id> ; <corpo> .

<seletor> ::= ( "[" <expressão> "]" )*
	
<termo> ::= <fator> (<op-mul> <fator>)* 


<tipo> ::= <tipo-agregado> | <tipo-simples>

<tipo-agregado> ::= array [ <literal> .. <literal> ] of <tipo>

<tipo-simples> ::= 
	integer 
	| real 
	| boolean

<variável> ::= 
	<id> <seletor>

<vazio> ::= ε

<float-lit> ::= <digito> <digito>* .  <digito>* | . <digito><digito>* 
